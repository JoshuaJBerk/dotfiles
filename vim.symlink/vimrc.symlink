" Maintainer: Joshua Berk <joshua.j.berk@gmail.com>

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Basic Settings                                                             "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Vim Defaults
set nocompatible             " vim settings, rather than vi
set nomodeline               " avoid self-executed security vulnerabilities
set secure                   " prevent auto-commands in foreign files

" Indentation + Formatting
set expandtab                " use spaces, not tabs
set smarttab                 " insert tabs @ line start re: shiftwidth
set tabstop=4                " display tabs as X spaces
set softtabstop=4            " insert mode creates X spaces
set shiftwidth=4             " reindent operations (<< && >>)
set shiftround               " use multiple of shiftwidth with '<' and '>'
set formatoptions+=tcqj      " see ':help fo-table' for details

" Search/Substitution/Completion
set hlsearch                 " highlight search results
set incsearch                " do incremental searching
set ignorecase               " removes case sensitivity by default
set smartcase                " adding ≥1 uppercase = case sensitivity
set gdefault                 " substitute all matches in line (not first)
set showmatch                " quickly jump to matching bracket
set cpt=.,w,b,u,t,i          " complete from buffers, files, and tags
set cot=menu,longest         " disable preview window on completion
set wig+=tmp/**              " wildmode: ignore tmp files

" View
set listchars=tab:▸\ ,trail:·,extends:❯,precedes:❮,nbsp:¤,eol:¬
set list                     " display formatting characters
set wrap                     " wrap lines
set showbreak=↪\             " indicator for wrapped lines
set nu rnu                   " add numbered lines (option: relativenumber)
set so=3 siso=0              " keep 3 lines off the edges during scroll
set nospell                  " spelling correction mode
set ttyfast                  " improves redrawing
set lazyredraw               " don't redraw during macros/etc.
set nosol                    " leave the cursor alone, Vim!
set ruler                    " display cursor position all the time
set cul                      " cursorline highlighting
set virtualedit=block        " allow cursor everywhere in V-Block mode
set laststatus=2             " always display status bar
set report=0                 " display all file changes
set showmode                 " display Insert/Visual/etc.
set showcmd                  " display incomplete commands
set title                    " filename in the window bar
set mouse=a                  " enable mouse support in the console
set mousehide                " hide pointer during typing, until moved
set encoding=utf-8           " UTF-8 encoding
set foldmethod=syntax        " collapsible code segments
set bs=indent,eol,start      " allow backspacing in insert mode
set shm+=aI                  " avoid 'press Enter to continue'
set t_Co=256                 " pretty colors everywhere whooo!!
set spr sb                   " split windows open below/right

" Backup
set undofile                 " persistent save of undo history
set udir=$TMPDIR             " store undo in System TMP Directory
set bdir=$TMPDIR             " store backup in System TMP Directory
set vdir=$TMPDIR             " store views in System TMP Directory
set dir=$TMPDIR              " store swap in System TMP Directory
set history=50               " # of commands to keep
set ut=2000                  " time to initiate swap/CursorHold AU
set autoread                 " updates VIM file if changed elsewhere
set hidden                   " hide buffers instead of closing them

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Plugins                                                                    "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" initiate w/pathogen package manager
call pathogen#infect()
call pathogen#helptags()

" Macros
runtime macros/matchit.vim

" Plugins
let g:neocomplete#enable_at_startup = 1
let g:investigate_use_dash=1
let g:syntastic_auto_loc_list=1
let g:syntastic_enable_signs=1
let g:sneak#streak = 1

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Functions                                                                  "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

function! InsertTabWrapper()
  let col = col('.') - 1
  if !col || getline('.')[col - 1] !~ '\k'
    return "\<tab>"
  else
    return "\<c-p>"
  endif
endfunction

function! SortFile()
  normal! mzggvG
  :sort ui
  normal! v`z
endfunction

function! ClearWhitespace()
  normal mz
  silent! %s/\s\+$//
  let @/=""
  update
  normal `z
endfunction

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Key Mappings                                                               "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Use Q for VIM CLI History
no Q q:

" Tab For All The Things!
ino <tab> <c-r>=InsertTabWrapper()<CR>

" Redraw screen + remove search highlights
nn <C-L> :nohl<CR><C-L>

" Force root permission saves
cmap w!! w !sudo tee > /dev/null %

" Line Navigation
no H ^
no L g_

" Reselect Visual Block after Indent/Outdent
vn < <gv
vn > >gv

" Set MapLeaders
let mapleader=","
let maplocalleader = "\\"

" Leader Key Maps
map <leader>y "*y<CR>
nn <leader><leader> <c-^>
nn <silent><leader><space> <Nop>
nn <silent><leader>; <Nop>
nn <silent><leader>/ <Nop>
nn <silent><leader>ul :t.<CR>Vr=
nn <silent><leader>cd :lcd %:h<CR>
nn <silent><leader>rc :so ~/.vimrc<CR>
nn <silent><leader>ss :setlocal spell!<CR>
nn <silent><leader>sp :setlocal paste!<CR>
nn <silent><leader>sf :call SortFile()<CR>
nn <silent><leader>ww :call ClearWhitespace()<CR>
nn <silent><leader>fc <ESC>/\v^[<=>]{7}( .*\|$)<CR>
nn <silent><leader>q/ :execute 'vimgrep /'.@/.'/g %'<CR>:copen<CR>
nn <silent><leader>fef :normal! gg=G``<CR>

" Plugin Key Maps
no <leader>a :Ack<space>
no <leader>t :Tabularize /
no <leader>k :call investigate#Investigate()<CR>
no <silent><leader>nt :NERDTreeToggle<CR>
no <silent><leader>tt :TagbarToggle<CR>
no <silent><leader>ut :GundoToggle<CR>
no <silent><leader>gg :GitGutterToggle<CR>
no <silent><leader>gs :Gstatus<CR>
no <silent><leader>gd :Gdiff<CR>
no <silent><leader>gc :Gcommit<CR>
no <silent><leader>gb :Gblame<CR>
no <silent><leader>gl :Glog<CR>
no <silent><leader>gp :Git push<CR>
no <silent><leader>xt :XtermColorTable<CR>

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Auto Commands                                                              "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

if has("autocmd")
  " Enable file type detection + use the default filetype settings
  " Also load indent files, to automatically do language-dependent indenting.
  filetype plugin indent on
  syntax on

  augroup vimrcEx
  au!

  " Exit 'Insert Mode' after UpdateTime
  au CursorHoldI * stopinsert

  " Filetype settings (tabs, spaces, etc.)
  au FileType python,css,markdown setl et sts=4 ts=4 shiftwidth=4 tw=79
  au FileType ruby,javascript,html,xml setl et sts=2 ts=2 shiftwidth=2

  " Improve Filetype detection
  au BufNewFile,BufRead *.md setf markdown
  au BufNewFile,BufRead *.json setf javascript

  " Always jump to the last known cursor position.
  au BufReadPost *
    \ if line("'\"") > 1 && line("'\"") <= line("$") |
    \   exe "normal! g`\"" |
    \ endif

  augroup END

endif

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Color Scheme                                                               "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Change Background by Time of Day
let hour = strftime("%H")
if hour >= 7 && hour <= 19
  set background=light
else
  set background=dark
endif

" Modifications
colo Solarized
hi clear SignColumn
hi clear SpecialKey

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" End of Settings                                                            "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
