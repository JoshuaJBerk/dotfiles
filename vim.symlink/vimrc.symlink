" Maintainer: Joshua Berk <joshua.j.berk@gmail.com>

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Basic Settings                                                             "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Vim Defaults
set nocompatible             " vim settings, rather than vi
set nomodeline               " avoid self-executed security vulnerabilities

" Indentation + Formatting
set expandtab                " use spaces, not tabs
set smarttab                 " insert tabs @ line start re: shiftwidth
set tabstop=4                " display tabs as X spaces
set softtabstop=4            " insert mode creates X spaces
set shiftwidth=4             " reindent operations (<< && >>)
set shiftround               " use multiple of shiftwidth with '<' and '>'
set formatoptions+=tcqj      " see ':help fo-table' for details

" Search/Substitution/Completion
set hlsearch                 " highlight search results
set incsearch                " do incremental searching
set ignorecase               " removes case sensitivity by default
set smartcase                " adding ≥1 uppercase = case sensitivity
set gdefault                 " substitute all matches in line (not first)
set showmatch                " quickly jump to matching bracket
set cpt=.,w,b,u,t,i          " complete from buffers, files, and tags
set cot=menu,longest         " disable preview window on completion
set wmnu                     " command-mode tab to auto-complete
set wim=longest:full,full    " wildmode: complete & list full matches
set wig+=tmp/**              " wildmode: ignore tmp files

" View
set listchars=tab:▸\ ,trail:·,extends:❯,precedes:❮,nbsp:¤,eol:¬
set list                     " display formatting characters
set wrap                     " wrap lines
set showbreak=↪\             " indicator for wrapped lines
set nu rnu                   " add numbered lines (option: relativenumber)
set ttyfast                  " improves redrawing
set lazyredraw               " don't redraw during macros/etc.
set so=3 siso=0              " keep 3 lines off the edges during scroll
set laststatus=2             " always display status bar
set nosol                    " leave the cursor alone, Vim!
set ruler                    " show the cursor position all the time
set cul                      " cursorline highlighting
set showmode                 " display Insert/Visual/etc.
set showcmd                  " display incomplete commands
set virtualedit=block        " allow cursor everywhere in V-Block mode
set mouse=a                  " enable mouse support in the console
set mousehide                " hide pointer during typing, until moved
set encoding=utf-8           " UTF-8 encoding
set foldmethod=syntax        " collapsible code segments
set bs=indent,eol,start      " allow backspacing in insert mode
set nospell                  " spelling correction mode
set shm=a                    " avoid 'press Enter to continue'
set t_Co=256                 " pretty colors everywhere whooo!!
set spr sb                   " split windows open below/right

" Backup
set undofile                 " persistent save of undo history
set udir=~/.vim/cache        " store undo in dotfiles
set bdir=~/.vim/cache        " store backup in dotfiles
set vdir=~/.vim/cache        " store views in dotfiles
set dir=~/.vim/cache         " store swap in dotfiles
set history=50               " # of commands to keep
set ut=2000                  " time to initiate swap/CursorHold AU
set autoread                 " updates VIM file if changed elsewhere
set hidden                   " hide buffers instead of closing them

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Plugins                                                                    "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" initiate w/pathogen package manager
call pathogen#infect()
call pathogen#helptags()

" Macros
runtime macros/matchit.vim

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Functions                                                                  "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

function! InsertTabWrapper()
  let col = col('.') - 1
  if !col || getline('.')[col - 1] !~ '\k'
    return "\<tab>"
  else
    return "\<c-p>"
  endif
endfunction

function! SortFile()
  normal! mzggvG
  :sort ui
  normal! v`z
endfunction

function! ClearWhitespace()
  normal mz
  silent! %s/\s\+$//
  let @/=""
  update
  normal `z
endfunction

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Key Mappings                                                               "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Use Q for VIM CLI History
no Q q:

" Tab For All The Things!
ino <tab> <c-r>=InsertTabWrapper()<cr>

" Force root permission saves
cno w!! w !sudo tee % >/dev/null

" Redraw screen + remove search highlights
nn <C-L> :nohl<CR><C-L>

" Line Navigation
no H ^
no L g_

" Arrow Keys
no <UP>  <Nop>
no <DOWN>  <Nop>
no <LEFT> <silent>:bp<cr>
no <RIGHT> <silent>:bn<cr>

" Set MapLeaders
let mapleader=","
let maplocalleader = "\\"

" Leader Key Maps
map <leader>y "*y<cr>
nn <leader><leader> <c-^>
nn <silent><leader>rc :so ~/.vimrc<cr>
nn <silent><leader>sf :call SortFile()<cr>
nn <silent><leader>pp :setlocal paste!<cr>
nn <silent><leader>W :call ClearWhitespace()<CR>

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Auto Commands                                                              "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

if has("autocmd")
  " Enable file type detection + use the default filetype settings
  " Also load indent files, to automatically do language-dependent indenting.
  filetype plugin indent on
  syntax on

  augroup vimrcEx
  au!

  " Exit 'Insert Mode' after UpdateTime
  au CursorHoldI * stopinsert

  " Filetype settings (tabs, spaces, etc.)
  au FileType python,css,markdown setl et sts=4 ts=4 shiftwidth=4 tw=79
  au FileType ruby,javascript,html,xml setl et sts=2 ts=2 shiftwidth=2

  " Improve Filetype detection
  au BufNewFile,BufRead *.md setf markdown
  au BufNewFile,BufRead *.json setf javascript

  " Always jump to the last known cursor position.
  au BufReadPost *
    \ if line("'\"") > 1 && line("'\"") <= line("$") |
    \   exe "normal! g`\"" |
    \ endif

  augroup END

else
  set autoindent
endif " has("autocmd")

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Color Scheme                                                               "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Change Background by Time of Day
let hour = strftime("%H")
if hour >= 7 && hour <= 19
  set background=light
else
  set background=dark
endif

" Modifications
colo Solarized
hi StatusLine term=bold ctermfg=236 ctermbg=231
hi clear SignColumn
hi clear SpecialKey

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" End of Settings                                                            "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
